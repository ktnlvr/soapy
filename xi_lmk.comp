#version 460
#extension GL_ARB_gpu_shader_fp64 : enable

/*
def xi_lmk_offset(l, m, k):
    offset_l = (l * (l + 1) * (l + 2)) // 6
    offset_m = (m * (2*l - m + 3)) // 2
    offset_k = k - m
    return offset_l + offset_m + offset_k
*/

layout(local_size_x = 1, local_size_y = 1, local_size_z = 1) in;
layout(std430, binding = 0) buffer Output { float xi_table[]; } output_buffer;
double dlog(double x) { return double(log(float(x))); }
double dexp(double x) { return double(exp(float(x))); }
double dsin(double x) { return double(sin(float(x))); }

double gammad(double zd) {
    const double PI = 3.1415926535897932384626433832795;
    const double p[9] = double[9](
        0.99999999999980993,
        676.5203681218851,
        -1259.1392167224028,
        771.32342877765313,
        -176.61502916214059,
        12.507343278686905,
        -0.13857109526572012,
        9.9843695780195716e-6,
        1.5056327351493116e-7
    );

    bool reflect = (zd < 0.5);
    double origZ = zd;
    double z = reflect ? (1.0 - zd) : zd;
    z -= 1.0;

    double x = p[0];
    for (int i = 1; i < 9; ++i)
        x += p[i] / (z + double(i));

    double t = z + 7.5;
    double g = sqrt(2.0 * PI) * dexp((z + 0.5) * dlog(t) - t) * x;

    if (reflect)
        g = PI / (dsin(PI * origZ) * g);

    return g;
}


int xi_lmk_offset(int l, int m, int k) {
    int offset_l = (l * (l + 1) * (l + 2)) / 6;
    int offset_m = (m * (2 * l - m + 3)) / 2;
    int offset_k = k - m;
    return offset_l + offset_m + offset_k;
}

void main() {
    uint l = gl_GlobalInvocationID.x;
    uint m = gl_GlobalInvocationID.y;
    uint k = gl_GlobalInvocationID.z;

    if (l == 0 && m == 0 && k == 0) {
        output_buffer.xi_table[0] = 1.0;
        return;
    }

    if (m > l || k < m || k > l)
        return;

    double value = 0.0;
    if (int(k - l) % 2 == 0) {
        double num = gammad((double(l + k - 1) / 2.0) + 1.0);
        double den = gammad(k - m + 1.0)
                   * gammad(l - k + 1.0)
                   * gammad((double(l + k - 1) / 2.0) - double(l) + 1.0);
        value = num / den;
    }

    int offset = xi_lmk_offset(int(l), int(m), int(k));
    output_buffer.xi_table[offset] = float(value);  // cast down to float for storage
}
